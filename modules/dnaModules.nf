#!/usr/bin/env nextflow
nextflow.enable.dsl = 2


date=new Date().format( 'yyMMdd' )
date2=new Date().format( 'yyMMdd HH:mm:ss' )
user="$USER"
runID="${date}.${user}"

/*
log.info """\
======================================================
Clinical Genetics Vejle: PacBio LRS v3
======================================================
Genome        : $params.genome
GenomeDir     : $refFilesDir
Input Readset : $inputReadSet_allDefault
read Subset   : $readSubset_hifiDefault
RunID         : $runID
Script start  : $date2
Genome FASTA  : ${genome_fasta}
Archive RAW   : ${dataArchive}
OutputDirBase : ${outputDirBase}
workDir       : ${workflow.workDir}
layout        : $params.layoutMode
min input GB  : $params.minGB
"""
*/

////////////////////////////////////////////
/////// ------- PREPROCESS + ALN ------- ///
////////////////////////////////////////////
process check_tmpdir {
    label "low"
    script:
    """
    echo "TMPDIR is: \$TMPDIR"
    df -h \$TMPDIR
    """
}

process write_input_summary {
    publishDir "${outputDirBase}/runInfo/${date}_${ssBase}/", mode: 'copy', pattern: "*.txt"
    publishDir "${lrsDocuments}/summaryData/allSamples/", mode: 'copy', pattern: "*.txt"

    input:
    val(summary_ch)

    output:
    path("*.txt")

    script:
    """
    cat > ${ssBase}.${inputReadSet_allDefault}.input.allSamples.summary.txt << 'EOF'
    ${summary_ch}
    """
}

process write_dropped_samples_summary {
    publishDir "${outputDirBase}/runInfo/${date}_${ssBase}/", mode: 'copy', pattern: "*.txt"
    publishDir "${lrsDocuments}/summaryData/droppedSamples/", mode: 'copy', pattern: "*.txt"
    input:
    val(summary_ch)

    output:
    path("*.txt")

    script:
    """
    cat > ${ssBase}.${inputReadSet_allDefault}.dropped.samples.summary.txt << 'EOF'
    ${summary_ch}
    """
}

process write_analyzed_samples_summary {
    publishDir "${outputDirBase}/runInfo/${date}_${ssBase}/", mode: 'copy', pattern: "*.txt"
    publishDir "${lrsDocuments}/summaryData/analyzedSamples/", mode: 'copy', pattern: "*.txt"
    input:
    val(summary_ch)

    output:
    path("*.txt")

    script:
    """
    cat > ${ssBase}.${inputReadSet_allDefault}.analyzed.samples.summary.txt << 'EOF'
    ${summary_ch}
    """
}


process create_fofn {
    label "low"
    
    publishDir {"${params.outBase(meta)}/documents/"}, mode: 'copy',pattern: '*.fofn',overwrite: true

    cpus 4
    input:
    tuple val(meta), path(data) //ubam

    output:
    tuple val(meta), path("${meta.id}.fofn")
    script:
    """
    `realpath ${data} > ${meta.id}.fofn`
    """
} 

process inputFiles_symlinks_ubam{
    label "low"
    publishDir {"${params.outBase(meta)}/documents/inputSymlinks/"}, mode: 'symlink', pattern: '*.{bam,pbi}',overwrite: true

    
    input:
    tuple val(meta), path(data)   

    output:
    tuple val(meta), path(data)
 
    script:
    """
    """

}
process symlinks_ubam_dropped {
    label "low"
    
    publishDir "${outputDirBase}/runInfo/${date}_${ssBase}/dropped_samples_ubam_symlinks/", mode: 'symlink', pattern: '*.{bam,pbi}',overwrite: true


    input:
    tuple val(meta), path(data)   

    output:
    tuple val(meta), path(data)
 
    script:
    """
    """
}

process pbmm2_align {
    label "veryHigh"
    tag "$meta.id"
    conda "${params.pbmm2}"
    
    input:
    tuple val(meta), path(data)
    
    output:
    tuple val(meta), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.pbmm2.bam"), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.pbmm2*bai"),  emit: bam
 
    script:
    """
    pbmm2 align \
    --preset HIFI \
    --sort \
    --num-threads ${task.cpus} \
    --bam-index BAI \
    --sample ${meta.id} \
    ${genome_mmi} \
    ${data[0]} \
    ${meta.id}.${genome_version}.${inputReadSet_allDefault}.pbmm2.bam
    """
}

process pbmm2_align_mergedData {
    label "veryHigh"
    tag "$meta.id"
    conda "${params.pbmm2}"

    input:
    tuple val(meta), path(fofn)
    
    output:
    tuple val(meta), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.pbmm2.merged.bam"), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.pbmm2.merged*bai"),  emit: bamAll


    script:
    """
    pbmm2 align \
    --preset HIFI \
    --sort \
    --num-threads ${task.cpus} \
    --bam-index BAI \
    --sample ${meta.id} \
    ${genome_mmi} \
    ${fofn} \
    ${meta.id}.${genome_version}.${inputReadSet_allDefault}.pbmm2.merged.bam
    """
}

process extractHifi {
    label "high"
    tag "$meta.id"
    conda "${params.pbtk}"

    input:
    tuple val(meta), path(bam),path(bai)

    output:
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.pbmm2.merged.bam"), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.pbmm2.merged*bai"),  emit: alignedHifi

    script:
    """
    extracthifi \
    -j ${task.cpus} \
    ${bam} \
    ${meta.id}.${genome_version}.${readSubset_hifiDefault}.pbmm2.merged.bam

    samtools index -@ ${task.cpus} ${meta.id}.${genome_version}.${readSubset_hifiDefault}.pbmm2.merged.bam
    """

}

////////////////////////////////////////////
/////// ------- SMALL VARIANTS ------- /////
////////////////////////////////////////////

process deepvariant{
    label "veryHigh"
    tag "$meta.id"

    publishDir "${lrsStorage}/deepVariant/gvcf/", mode: 'copy', pattern: "*.deepVariant.g.vcf.*"    
    publishDir {"${params.outBase(meta)}/SNV_and_INDELs/gvcf/"}, mode: 'copy', pattern: "*.deepVariant.g.vcf.*"


    input:
    tuple val(meta), val(data)

    output:
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.deepVariant.vcf.gz"), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.deepVariant.vcf.gz.tbi"), emit: dv_vcf

    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.deepVariant.g.vcf.gz"), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.deepVariant.g.vcf.gz.tbi"), emit: dv_gvcf    

    """
    singularity run -B ${s_bind} ${simgpath}/deepvariant190.sif /opt/deepvariant/bin/run_deepvariant \
    --model_type=PACBIO \
    --ref=${genome_fasta} \
    --reads=${data.mainBamFile} \
    --output_vcf=${meta.id}.${genome_version}.${readSubset_hifiDefault}.deepVariant.vcf.gz \
    --output_gvcf=${meta.id}.${genome_version}.${readSubset_hifiDefault}.deepVariant.g.vcf.gz \
    --num_shards=${task.cpus}
    """    
}

process glNexus_jointCall { 
    label "high"
    tag "$meta.caseID"
    conda "${params.glnexus}"
    publishDir {"${params.outBase(meta)}/jointCalls/"}, mode: 'copy', pattern: "*.jointCall.*"
    publishDir {"${params.outBase(meta)}/documents/"}, mode: 'copy', pattern: "*.manifest"

    input:
    tuple val(meta), path(manifest)

    output:
    tuple val(meta), path("${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.deepVariant.jointCall.vcf.gz"), path("${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.deepVariant.jointCall.vcf.gz.tbi"), emit: glnexus_vcf
    tuple val(meta), path("${manifest}")
    tuple val(meta), path("${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.deepVariant.jointCall.WES_ROI.vcf.gz"), path("${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.deepVariant.jointCall.WES_ROI.vcf.gz.tbi"),emit:glnexus_wes_roi_vcf
    
    script:
    """
    glnexus_cli \
    --config DeepVariant \
    --threads ${task.cpus} \
    --list ${manifest} > ${meta.caseID}.glnexus.bcf

    bcftools view -Oz -o ${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.deepVariant.jointCall.vcf.gz ${meta.caseID}.glnexus.bcf
    bcftools index -t ${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.deepVariant.jointCall.vcf.gz

    bcftools view -R ${ROI} ${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.deepVariant.jointCall.vcf.gz -Oz -o ${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.deepVariant.jointCall.WES_ROI.vcf.gz
    bcftools index -t ${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.deepVariant.jointCall.WES_ROI.vcf.gz

    """
}


///////////////////////////////////////////////////
////// -------PHASING ------- /////////////////////
///////////////////////////////////////////////////


process hiPhaseTwoAln {
    
    tag "$meta.id"
    label "intermediate"
    conda "${params.hiphase}"
    publishDir {"${params.outBase(meta)}/alignments/HifiReads/"}, mode: 'copy', pattern: "*.${readSubset_hifiDefault}.hiphase.ba*"

    publishDir {"${params.outBase(meta)}/alignments/allReads/"}, mode: 'copy', pattern: "*.${inputReadSet_allDefault}.hiphase.cra*"

    publishDir {"${params.outBase(meta)}/SNV_and_INDELs/"}, mode: 'copy', pattern: "*.hiphase.deepvariant.*"

    publishDir {"${params.outBase(meta)}/repeatExpansions/TRGT/diseaseSTRs/"}, mode: 'copy', pattern: "*.hiphase.trgt4.*"

    publishDir "${lrsStorage}/deepVariant/vcfs/", mode: 'copy', pattern:"*.hiphase.deepvariant.vcf.*"

    input:
    tuple val(meta), val(data), path(vcf), path(sv), path(str)
    
    output:
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.bam"), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.bam.bai"),  emit: hiphase_bam                 
   
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.cram"), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.cram.crai"), emit: hiphase_cram       
    
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.deepvariant.vcf.gz"), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.deepvariant.vcf.gz.tbi"), emit: hiphase_dv_vcf

    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.deepvariant.WES_ROI.vcf.gz"), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.deepvariant.WES_ROI.vcf.gz.tbi")

    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.sawfish.vcf.gz"), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.sawfish.vcf.gz.tbi"), emit: hiphase_sawfish_vcf
   
    tuple val(meta), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.hiphase.trgt4.STRchive.sorted.vcf.gz"), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.hiphase.trgt4.STRchive.sorted.vcf.gz.tbi"), emit: hiphase_trgt_vcf
    tuple val(meta), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.hiphase.bam"), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.hiphase.bam.bai")//,  emit: hiphase_allReads_bam  

    tuple val(meta), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.hiphase.cram"), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.hiphase.cram.crai")//,  emit: hiphase_allReads_cram  


    script:
    def bamArgs = []
    def hiphaseBams = []
    if (params.allReads || params.hifiReads || params.failedReads && (!params.skipAllReadsCRAM)) {
        bamArgs += [
            "--bam ${data.mainBamFile}",
            "--output-bam ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.bam"
        ]
        hiphaseBams += [
            "${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.bam"
        ]
    }
    else {
        bamArgs += [
            "--bam ${data.mainBamFile}",
            "--output-bam ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.bam",
            "--bam ${data.bamAll}",
            "--output-bam ${meta.id}.${genome_version}.${inputReadSet_allDefault}.hiphase.bam"
        ]
        hiphaseBams += [
            "${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.bam",
            "${meta.id}.${genome_version}.${inputReadSet_allDefault}.hiphase.bam"
        ]
    }
    def bamArgsStr = bamArgs.join(' ')

    def cramCmds = hiphaseBams.collect { bam ->
    def cram = bam.replaceFirst(/\.bam$/, '.cram')
    """
    samtools view -@ ${task.cpus} -T ${genome_fasta} --write-index -C -O cram,version=3.1,level=6 -o ${cram} ${bam}
    """.stripIndent().trim()
    }.join("\n\n")


    """
    hiphase \
    $bamArgsStr \
    --vcf ${vcf[0]} \
    --output-vcf ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.deepvariant.vcf.gz \
    --vcf ${sv[0]} \
    --output-vcf ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.sawfish.vcf.gz \
    --vcf ${str[0]} \
    --output-vcf ${meta.id}.${genome_version}.${inputReadSet_allDefault}.hiphase.trgt4.STRchive.sorted.vcf.gz \
    --reference ${genome_fasta} \
    --threads ${task.cpus} \
    --io-threads ${task.cpus}

    bcftools index -t -f ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.deepvariant.vcf.gz

    ${gatk_exec} SelectVariants \
    -R ${genome_fasta} \
    -V  ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.deepvariant.vcf.gz \
    -L ${ROI} \
    -O  ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.deepvariant.WES_ROI.vcf.gz

    ${cramCmds}

    """
}


process hiPhase {
    
    tag "$meta.id"
    label "intermediate"
    conda "${params.hiphase}"
    publishDir {"${params.outBase(meta)}/alignments/HifiReads/"}, mode: 'copy', pattern: "*.${readSubset_hifiDefault}.hiphase.ba*"

    publishDir {"${params.outBase(meta)}/SNV_and_INDELs/"}, mode: 'copy', pattern: "*.hiphase.deepvariant.*"

    publishDir {"${params.outBase(meta)}/repeatExpansions/TRGT/diseaseSTRs/"}, mode: 'copy', pattern: "*.hiphase.trgt4.*"

    publishDir "${lrsStorage}/deepVariant/vcfs/", mode: 'copy', pattern:"*.hiphase.deepvariant.vcf.*"

    input:
    tuple val(meta), val(data), path(vcf), path(sv), path(str)
    
    output:
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.bam"), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.bam.bai"),  emit: hiphase_bam                 
   
     
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.deepvariant.vcf.gz"), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.deepvariant.vcf.gz.tbi"), emit: hiphase_dv_vcf

    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.deepvariant.WES_ROI.vcf.gz"), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.deepvariant.WES_ROI.vcf.gz.tbi")

    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.sawfish.vcf.gz"), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.sawfish.vcf.gz.tbi"), emit: hiphase_sawfish_vcf
   
    tuple val(meta), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.hiphase.trgt4.STRchive.sorted.vcf.gz"), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.hiphase.trgt4.STRchive.sorted.vcf.gz.tbi"), emit: hiphase_trgt_vcf

    script:
    """
    hiphase \
    --bam ${data.mainBamFile} \
    --output-bam ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.bam \
    --vcf ${vcf[0]} \
    --output-vcf ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.deepvariant.vcf.gz \
    --vcf ${sv[0]} \
    --output-vcf ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.sawfish.vcf.gz \
    --vcf ${str[0]} \
    --output-vcf ${meta.id}.${genome_version}.${inputReadSet_allDefault}.hiphase.trgt4.STRchive.sorted.vcf.gz \
    --reference ${genome_fasta} \
    --threads ${task.cpus} \
    --io-threads ${task.cpus}

    bcftools index -t -f ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.deepvariant.vcf.gz

    ${gatk_exec} SelectVariants \
    -R ${genome_fasta} \
    -V  ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.deepvariant.vcf.gz \
    -L ${ROI} \
    -O  ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.deepvariant.WES_ROI.vcf.gz
    """
}



///////////////////////////////////////////////////
////// -------CNV AND STRUCTURAL VARIANTS ------- /
///////////////////////////////////////////////////

process sawFish2 {
    tag "$meta.id"
    label "high"
    conda "${params.sawfish2}"

    publishDir {"${params.outBase(meta)}/structuralVariants/${meta.id}.sawfishSV/supportingFiles/"}, mode: 'copy', pattern: "*.{bedgraph,bw,json,json.gz}"
    publishDir {"${params.outBase(meta)}/structuralVariants/${meta.id}.sawfishSV/"}, mode: 'copy', pattern: "${meta.id}.sawfishDiscover"

    input:
    tuple val(meta), val(data)
    
    output:
    tuple val(meta),  path("*.sawfishSV.*") //path(data),
    tuple val(meta), path("*.sawfishSV.vcf.gz"),path("*.sawfishSV.vcf.gz.tbi")      , emit: sv_vcf
    //path("${meta.id}.sawfishDiscover")                                              , emit: sv_discover_dir
    tuple val(meta), path("${meta.id}.sawfishDiscover"), val("${data.mainBamFile}") , emit: sv_discover_dir2
    tuple val(meta), path("*.sawfishSV.supporting_reads.json.gz")                   , emit: sv_supporting_reads
    tuple val(meta), path("${meta.id}.sawfishSV/")                                  , emit: sawfish_out_dir

    script:
    def exclude=params.genome=="hg38" ? "--cnv-excluded-regions ${cnv_exclude_sawfish}" : ""
    def sex = (meta.sex=="male"||meta.sex=="M"||meta.genderFile=="M") ? "--expected-cn ${sawfishMaleExpectedCN}" : "--expected-cn ${sawfishFemaleExpectedCN}"
   
    """
    sawfish discover \
    --threads ${task.cpus} \
    --ref ${genome_fasta} \
    --bam ${data.mainBamFile} \
    $exclude \
    $sex \
    --output-dir ${meta.id}.sawfishDiscover 

    sawfish joint-call \
    --threads ${task.cpus} \
    --report-supporting-reads \
    --sample ${meta.id}.sawfishDiscover \
    --output-dir ${meta.id}.sawfishSV 
    
    mv ${meta.id}.sawfishSV/genotyped.sv.vcf.gz ${meta.id}.${genome_version}.${readSubset_hifiDefault}.sawfishSV.vcf.gz

    mv ${meta.id}.sawfishSV/genotyped.sv.vcf.gz.tbi ${meta.id}.${genome_version}.${readSubset_hifiDefault}.sawfishSV.vcf.gz.tbi

    mv ${meta.id}.sawfishSV/supporting_reads.json.gz ${meta.id}.${genome_version}.${readSubset_hifiDefault}.sawfishSV.supporting_reads.json.gz

    mv ${meta.id}.sawfishSV/samples/*/gc_bias_corrected_depth.bw ${meta.id}.${genome_version}.${readSubset_hifiDefault}.sawfishSV.gc_bias_corrected_depth.bw

    mv ${meta.id}.sawfishSV/samples/*/depth.bw ${meta.id}.${genome_version}.${readSubset_hifiDefault}.sawfishSV.depth.bw

    mv ${meta.id}.sawfishSV/samples/*/copynum.bedgraph ${meta.id}.${genome_version}.${readSubset_hifiDefault}.sawfishSV.copynum.bedgraph

    mv ${meta.id}.sawfishSV/samples/*/copynum.summary.json ${meta.id}.${genome_version}.${readSubset_hifiDefault}.sawfishSV.copynum.summary.json
    """
}


process svdb_SawFish {
    tag "$meta.id"
    label "low"
    conda "${params.svdb}"

    publishDir "${lrsStorage}/structuralVariants/sawfish/", mode: 'copy',pattern: "*.sawfishSV.hiphase.svdb.vcf*"

    publishDir {"${params.outBase(meta)}/structuralVariants/"}, mode: 'copy', pattern: "*.sawfishSV.hiphase.svdb.*"

    input:
    tuple val(meta), val(data)
    
    output:
    tuple val(meta), path("*.sawfishSV.hiphase.svdb.*")
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.sawfishSV.hiphase.svdb.AF_below10pct.vcf.gz"),path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.sawfishSV.hiphase.svdb.AF_below10pct.vcf.gz.tbi"), emit: sawfishAF10
    script:
    """
    svdb --query \
    --query_vcf ${data.sawfish_vcf} \
    --sqdb ${sawfish_sqdb} > ${meta.id}.${genome_version}.${readSubset_hifiDefault}.sawfishSV.hiphase.svdb.vcf
    
    bgzip ${meta.id}.${genome_version}.${readSubset_hifiDefault}.sawfishSV.hiphase.svdb.vcf
    
    bcftools index -t ${meta.id}.${genome_version}.${readSubset_hifiDefault}.sawfishSV.hiphase.svdb.vcf.gz

    bcftools view -e 'INFO/FRQ>0.1' ${meta.id}.${genome_version}.${readSubset_hifiDefault}.sawfishSV.hiphase.svdb.vcf.gz -Oz -o ${meta.id}.${genome_version}.${readSubset_hifiDefault}.sawfishSV.hiphase.svdb.AF_below10pct.vcf.gz

    bcftools index -t ${meta.id}.${genome_version}.${readSubset_hifiDefault}.sawfishSV.hiphase.svdb.AF_below10pct.vcf.gz

    """
}

process sawFish2_jointCall_all{
    label "high"
    conda "${params.sawfish2}"


    input:
    val(x)
    
    output:
    tuple path("*.sawfishSV_jointCall.vcf.gz"),path("*.sawfishSV_jointCall.vcf.gz.tbi"),emit: sv_jointCall_vcf

    script:
    """
    sawfish joint-call \
    --threads ${task.cpus} \
    ${x} \
    --output-dir ${params.rundir}.sawfishSV_jointCall 
    
    mv ${params.rundir}.sawfishSV_jointCall/genotyped.sv.vcf.gz ${params.rundir}.${genome_version}.${readSubset_hifiDefault}.sawfishSV_jointCall.vcf.gz

    mv ${params.rundir}.sawfishSV_jointCall/genotyped.sv.vcf.gz.tbi ${params.rundir}.${genome_version}.${readSubset_hifiDefault}.sawfishSV_jointCall.vcf.gz.tbi
    """
}

process svdb_sawFish2_jointCall_all {
    label "low"
    conda "${params.svdb}"
    
    publishDir {"${params.outBase(meta)}/jointCalls_All/"}, mode: 'copy', pattern: "*_jointCall.svdb.*"


    input:
    tuple path(vcf), path(idx)
    
    output:
    path("*_jointCall.svdb.*")

    script:
    """
    svdb --query \
    --query_vcf ${vcf} \
    --sqdb ${sawfish_sqdb} > ${params.rundir}.${genome_version}.${readSubset_hifiDefault}.sawfishSV_jointCall.svdb.vcf
    bgzip ${params.rundir}.${genome_version}.${readSubset_hifiDefault}.sawfishSV_jointCall.svdb.vcf
    bcftools index -t ${params.rundir}.${genome_version}.${readSubset_hifiDefault}.sawfishSV_jointCall.svdb.vcf.gz

    bcftools view -e 'INFO/FRQ>0.1' ${params.rundir}.${genome_version}.${readSubset_hifiDefault}.sawfishSV_jointCall.svdb.vcf.gz -Oz -o ${params.rundir}.${genome_version}.${readSubset_hifiDefault}.sawfishSV_jointCall.svdb.AF_below10pct.vcf.gz
    bcftools index -t ${params.rundir}.${genome_version}.${readSubset_hifiDefault}.sawfishSV_jointCall.svdb.AF_below10pct.vcf.gz
    """
}

process sawFish2_jointCall_caseID{
    tag "$meta.caseID"
    label "high"
    conda "${params.sawfish2}"

    publishDir {"${params.outBase(meta)}/jointCalls/"}, mode: 'copy', pattern: "*.sawfishSV.hiphase.svdb.*"

    input:
    tuple val(meta), path(manifest)
    
    output:
    tuple val(meta), path("*.sawfishSV_jointCall.vcf.gz"),path("*.sawfishSV_jointCall.vcf.gz.tbi"),emit: sv_jointCall_caseID_vcf

    script:
    """
    sawfish joint-call \
    --threads ${task.cpus} \
    --sample-csv ${manifest} \
    --output-dir ${meta.caseID}.sawfishSV_jointCall 
    
    mv ${meta.caseID}.sawfishSV_jointCall/genotyped.sv.vcf.gz ${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.sawfishSV_jointCall.vcf.gz

    mv ${meta.caseID}.sawfishSV_jointCall/genotyped.sv.vcf.gz.tbi ${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.sawfishSV_jointCall.vcf.gz.tbi
    """
}

process svdb_sawFish2_jointCall_caseID {
    label "low"
    conda "${params.svdb}"
    
    publishDir {"${params.outBase(meta)}/jointCalls/"}, mode: 'copy', pattern: "*_jointCall.svdb.*"

    input:
    tuple val(meta), path(vcf), path(idx)
    
    output:
    path("*_jointCall.svdb.*")
    tuple val(meta), path("${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.sawfishSV_jointCall.svdb.AF_below10pct.vcf.gz"),path("${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.sawfishSV_jointCall.svdb.AF_below10pct.vcf.gz.tbi"), emit: sawfish_caseID_AF10
    script:
    """
    svdb --query \
    --query_vcf ${vcf} \
    --sqdb ${sawfish_sqdb} > ${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.sawfishSV_jointCall.svdb.vcf
    bgzip ${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.sawfishSV_jointCall.svdb.vcf
    bcftools index -t ${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.sawfishSV_jointCall.svdb.vcf.gz

    bcftools view -e 'INFO/FRQ>0.1' ${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.sawfishSV_jointCall.svdb.vcf.gz -Oz -o ${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.sawfishSV_jointCall.svdb.AF_below10pct.vcf.gz
    bcftools index -t ${meta.caseID}.${genome_version}.${readSubset_hifiDefault}.sawfishSV_jointCall.svdb.AF_below10pct.vcf.gz
    """
}

process svTopo {
    tag "$meta.id"
    conda "${params.svtopo}"
    cpus 4

    publishDir {"${params.outBase(meta)}/structuralVariants/SVtopo/"}, mode: 'copy'


    input:
    tuple val(meta), val(data)
    
    output:
    tuple val(meta), path("${meta.id}.svtopo_out/")
    script:
    def exclude=params.genome=="hg38" ? "--exclude-regions ${cnv_exclude_sawfish}" : ""
    """
    mkdir ${meta.id}.svtopo_out

    svtopo \
    --bam ${data.bam} \
    --vcf ${data.sawfish_vcf} \
    --variant-readnames ${data.sawfish_reads} \
    --prefix ${meta.id} \
    $exclude \
    --svtopo-dir ${meta.id}.svtopo_out/ 

    svtopovz \
    --svtopo-dir ${meta.id}.svtopo_out/ \
    --genes ${gencode_gtf} \
    --image-type jpg 

    mv ${meta.id}.svtopo_out/index.html ${meta.id}.svtopo_out/${meta.id}.sawfishSV.svtopo.html
    """
}

process svTopo_filtered {
    tag "$meta.id"
    label "high"
    conda "${params.svtopo}"

    publishDir {"${params.outBase(meta)}/structuralVariants/SVtopo_filtered/"}, mode: 'copy'



    input:
    tuple val(meta), val(data)
    
    output:
    tuple val(meta), path("${meta.id}.svtopo_out/")
    script:
    def exclude=params.genome=="hg38" ? "--exclude-regions ${cnv_exclude_sawfish}" : ""
    """
    mkdir ${meta.id}.svtopo_out

    svtopo \
    --bam ${data.bam} \
    --vcf ${data.sawfish10_vcf} \
    --variant-readnames ${data.sawfish_reads} \
    --prefix ${meta.id} \
    $exclude \
    --svtopo-dir ${meta.id}.svtopo_out/ 

    svtopovz \
    --svtopo-dir ${meta.id}.svtopo_out/ \
    --genes ${gencode_gtf} \
    --image-type jpg 

    mv ${meta.id}.svtopo_out/index.html ${meta.id}.svtopo_out/${meta.id}.sawfishSV.svtopo.html
    """
}


///////////////////////////////////////////////////
/////// ------- PSEUDO, VNTR, REPEATS, MITO ------- //
///////////////////////////////////////////////////


process mitorsaw {
    tag "$meta.id"
    label "medium"
    conda "${params.mitorsaw}"

 
    publishDir {"${params.outBase(meta)}/specialAnalysis/mitochondrialVariants/"}, mode: 'copy'



    input:
    tuple val(meta), val(data)
    
    output:
    tuple val(meta), path("*.mitorsaw.*")

    script:
    """
    mitorsaw haplotype \
    --reference ${genome_fasta} \
    --bam ${data.bam} \
    --output-vcf ${meta.id}.${genome_version}.${readSubset_hifiDefault}.mitorsaw.vcf.gz \
    --output-hap-stats ${meta.id}.${genome_version}.${readSubset_hifiDefault}.mitorsaw.hapstats.json 

    """
}

process trgt4_diseaseSTRs{
   
    tag "$meta.id"
    label "low"
    conda "${params.trgt4}"
    
    publishDir {"${params.outBase(meta)}/repeatExpansions/TRGT/bam"}, mode: 'copy', pattern: "*.sorted.ba*"

    publishDir "${lrsStorage}/STRs/repeatExpansions/TRGT/diseaseSTRs/", mode: 'copy', pattern:"*.sorted.vcf.*"

    input:
    tuple val(meta), val(data)
    
    output:
    tuple val(meta), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.STRchive.sorted.vcf.gz"), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.STRchive.sorted.vcf.gz.tbi"),emit: str4_vcf
    
    tuple val(meta),path ("*.sorted.*")

    tuple val(meta), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.STRchive.sorted.bam"), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.STRchive.sorted.bam.bai"), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.STRchive.sorted.vcf.gz"), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.STRchive.sorted.vcf.gz.tbi"),emit: trgt_full
    
    script:
    def karyotype=(meta.sex=="male"||meta.sex=="M"||meta.genderFile=="M") ? "--karyotype XY" : "--karyotype XX"
    
    def bamArgs 
    if (params.allReads || params.hifiReads|| params.failedReads) {
        bamArgs="--reads ${data.mainBamFile}"
    }
    else  {
        bamArgs="--reads ${data.bamAll}"
    }

    """
    trgt genotype \
    --genome ${genome_fasta} \
    --repeats ${tr_pathogenic_v2} \
    $bamArgs \
    $karyotype \
    --output-prefix ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.STRchive

    bcftools sort -Ov -o ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.STRchive.sorted.vcf.gz ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.STRchive.vcf.gz 
    bcftools index -t ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.STRchive.sorted.vcf.gz

    samtools sort -o ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.STRchive.sorted.bam ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.STRchive.spanning.bam
    samtools index ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.STRchive.sorted.bam
    """
}

process trgt4_diseaseSTRs_plots{
    tag "$meta.id"
    label "low"
    conda "${params.trgt4}"
    
    publishDir {"${params.outBase(meta)}/repeatExpansions/TRGT/Plots/"}, mode: 'copy', pattern: "*.{pdf,png,svg}"

    input:
    tuple val(meta), val(data)
    
    output:
    tuple val(meta), path("*.{pdf,png,svg}")


    script:
    def geneList = params.puretargetPlotGenes.join(' ')

    """
    for gene in ${geneList}; do
    trgt plot \
    --genome ${genome_fasta} \
    --repeats ${tr_pathogenic_v2} \
    --vcf ${data.vcf} \
    --spanning-reads ${data.bam} \
    --repeat-id \$gene \
    --squished \
    -o ${meta.id}.${genome_version}.${inputReadSet_allDefault}.\$gene.allele.pdf

    trgt plot \
    --genome ${genome_fasta} \
    --repeats ${tr_pathogenic_v2} \
    --vcf ${data.vcf} \
    --spanning-reads ${data.bam} \
    --repeat-id \$gene \
    --plot-type waterfall \
    -o ${meta.id}.${genome_version}.${inputReadSet_allDefault}.\$gene.waterfall.pdf
    done
    """
}



//--repeat-id ${data.strID} \
process trgt4_diseaseSTRs_plots_meth{
    tag "$meta.id"
    label "medium"
    conda "${params.trgt4}"

    publishDir {"${params.outBase(meta)}/repeatExpansions/TRGT/METHplots/"}, mode: 'copy', pattern: "*.{pdf,png,svg}"
    input:
    tuple val(meta), val(data)
    
    output:
    tuple val(meta), path("*.{pdf,png,svg}")
    script:

    """
    trgt plot \
    --genome ${genome_fasta} \
    --repeats ${tr_pathogenic_v2} \
    --vcf ${data.vcf} \
    --spanning-reads ${data.bam} \
    --repeat-id FXS_FMR1 \
    --show meth \
    --squished \
    --max-allele-reads 75 \
    -o FXS_FMR1.${meta.id}.${genome_version}.${readSet}.METH.alleleSquished.pdf

    trgt plot \
    --genome ${genome_fasta} \
    --repeats ${tr_pathogenic_v2} \
    --vcf ${data.vcf} \
    --spanning-reads ${data.bam} \
    --repeat-id FXS_FMR1 \
    --plot-type waterfall \
    --show meth \
    --max-allele-reads 75 \
    -o FXS_FMR1.${meta.id}.${genome_version}.${readSet}.METH.waterfall.pdf

    """
}


process trgt4_all {

    tag "$meta.id"
    label "high"
    conda "${params.trgt4}"
  
    publishDir {"${params.outBase(meta)}/repeatExpansions/TRGT/bam"}, mode: 'copy', pattern: "*.sorted.ba*"
    
    publishDir {"${params.outBase(meta)}/repeatExpansions/TRGT/allSTRs/"}, mode: 'copy', pattern: "*.sorted.vcf.*"

    publishDir "${lrsStorage}/STRs/repeatExpansions/TRGT/all/", mode: 'copy', pattern:"*.sorted.vcf.*"

    input:
    tuple val(meta), val(data)
    
    output:
    tuple val(meta), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.allSTR.sorted.bam"), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.allSTR.sorted.bam.bai"),emit: str_spanning_bam
    tuple val(meta), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.allSTR.sorted.vcf.gz"), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.allSTR.sorted.vcf.gz.tbi"),emit: str4All_vcf
    
    script:
    def karyotype=(meta.sex=="male"||meta.sex=="M"||meta.genderFile=="M")  ? "--karyotype XY" : "--karyotype XX"
    def readsInput= params.hifiReads ? "--reads ${data.mainBamFile}" : params.allReads ? "--reads ${data.mainBamFile}" : "--reads ${data.bamAll}"     

    """
    trgt genotype \
    --genome ${genome_fasta} \
    --repeats ${tr_all} \
    $readsInput \
    $karyotype \
    --output-prefix ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.allSTR

    bcftools sort -Ov -o ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.allSTR.sorted.vcf.gz ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.allSTR.vcf.gz 
    bcftools index -t ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.allSTR.sorted.vcf.gz

    samtools sort -o ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.allSTR.sorted.bam ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.allSTR.spanning.bam
    samtools index ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt4.allSTR.sorted.bam
    """
}

process trgt5_diseaseSTRs{
   
    tag "$meta.id"
    label "low"
    conda "${params.trgt5}"
    
    publishDir {"${params.outBase(meta)}/newToolsTest/repeatExpansions/TRGT5/bam"}, mode: 'copy', pattern: "*.sorted.ba*"
    publishDir {"${params.outBase(meta)}/newToolsTest/repeatExpansions/TRGT5/diseaseSTRs"}, mode: 'copy', pattern: "*.sorted.vcf*"

    //publishDir "${lrsStorage}/STRs/repeatExpansions/TRGT/diseaseSTRs/", mode: 'copy', pattern:"*.sorted.vcf.*"

    input:
    tuple val(meta), val(data)
    
    output:
    
    tuple val(meta),path ("*.sorted.*")

    tuple val(meta), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt5.STRchive.sorted.bam"), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt5.STRchive.sorted.bam.bai"), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt5.STRchive.sorted.vcf.gz"), path("${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt5.STRchive.sorted.vcf.gz.tbi"),emit: trgt_full
    
    script:
    def karyotype=(meta.sex=="male"||meta.sex=="M"||meta.genderFile=="M") ? "--karyotype XY" : "--karyotype XX"
    
    def bamArgs 
    if (params.allReads || params.hifiReads|| params.failedReads) {
        bamArgs="--reads ${data.mainBamFile}"
    }
    else  {
        bamArgs="--reads ${data.bamAll}"
    }

    """
    trgt genotype \
    --genome ${genome_fasta} \
    --repeats ${tr_pathogenic_v2} \
    $bamArgs \
    $karyotype \
    --output-prefix ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt5.STRchive

    bcftools sort -Ov -o ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt5.STRchive.sorted.vcf.gz ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt5.STRchive.vcf.gz 
    bcftools index -t ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt5.STRchive.sorted.vcf.gz

    samtools sort -o ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt5.STRchive.sorted.bam ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt5.STRchive.spanning.bam
    samtools index ${meta.id}.${genome_version}.${inputReadSet_allDefault}.trgt5.STRchive.sorted.bam
    """
}

process trgt5_diseaseSTRs_plots{
    tag "$meta.id"
    label "low"
    conda "${params.trgt5}"
    
    publishDir {"${params.outBase(meta)}/newToolsTest/repeatExpansions/TRGT5/Plots/"}, mode: 'copy', pattern: "*.{pdf,png,svg}"

    input:
    tuple val(meta), val(data)
    
    output:
    tuple val(meta), path("*.{pdf,png,svg}")


    script:

    def geneList = params.puretargetPlotGenes.join(' ')

    """
    for gene in ${geneList}; do
    trgt plot \
    --genome ${genome_fasta} \
    --repeats ${tr_pathogenic_v2} \
    --vcf ${data.vcf} \
    --spanning-reads ${data.bam} \
    --repeat-id \$gene \
    --squished \
    -o ${meta.id}.${genome_version}.${inputReadSet_allDefault}.\$gene.allele.pdf

    trgt plot \
    --genome ${genome_fasta} \
    --repeats ${tr_pathogenic_v2} \
    --vcf ${data.vcf} \
    --spanning-reads ${data.bam} \
    --repeat-id \$gene \
    --plot-type waterfall \
    -o ${meta.id}.${genome_version}.${inputReadSet_allDefault}.\$gene.waterfall.pdf
    done
    """
}


/*
    """
    trgt plot \
    --genome ${genome_fasta} \
    --repeats ${tr_pathogenic_v2} \
    --vcf ${data.vcf} \
    --spanning-reads ${data.bam} \
    --repeat-id ${data.strID} \
    --squished \
    -o ${meta.id}.${genome_version}.${inputReadSet_allDefault}.${data.strID}.allele.pdf

    trgt plot \
    --genome ${genome_fasta} \
    --repeats ${tr_pathogenic_v2} \
    --vcf ${data.vcf} \
    --spanning-reads ${data.bam} \
    --repeat-id ${data.strID} \
    --plot-type waterfall \
    -o ${meta.id}.${genome_version}.${inputReadSet_allDefault}.${data.strID}.waterfall.pdf
*/


process trgt5_diseaseSTRs_plots_meth{
    tag "$meta.id"
    label "medium"
    conda "${params.trgt5}"

    publishDir {"${params.outBase(meta)}/newToolsTest/repeatExpansions/TRGT/METHplots/"}, mode: 'copy', pattern: "*.{pdf,png,svg}"
    input:
    tuple val(meta), val(data)
    
    output:
    tuple val(meta), path("*.{pdf,png,svg}")
    script:

    """
    trgt plot \
    --genome ${genome_fasta} \
    --repeats ${tr_pathogenic_v2} \
    --vcf ${data.vcf} \
    --spanning-reads ${data.bam} \
    --repeat-id FXS_FMR1 \
    --show meth \
    --squished \
    --max-allele-reads 75 \
    -o FXS_FMR1.${meta.id}.${genome_version}.${readSet}.METH.alleleSquished.pdf

    trgt plot \
    --genome ${genome_fasta} \
    --repeats ${tr_pathogenic_v2} \
    --vcf ${data.vcf} \
    --spanning-reads ${data.bam} \
    --repeat-id FXS_FMR1 \
    --plot-type waterfall \
    --show meth \
    --max-allele-reads 75 \
    -o FXS_FMR1.${meta.id}.${genome_version}.${readSet}.METH.waterfall.pdf

    """
}



process kivvi_d4z4{
    tag "$meta.id"
    label "medium"

    publishDir {"${params.outBase(meta)}/repeatExpansions/Kivvi_D4Z4_contraction/"}, mode: 'copy'

    input:
    tuple val(meta), val(data)
   //  tuple val(meta), path(data)   
    output:
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.kivviD4Z4")
     
    script:

    """
    ${params.kivvi_dir}/kivvi \
    -r ${genome_fasta} \
    --bam ${data.bam} \
    -p ${meta.id}.${genome_version}.${readSubset_hifiDefault} \
    -o ${meta.id}.${genome_version}.${readSubset_hifiDefault}.kivviD4Z4 \
    d4z4
    """
}

process kivvi05_d4z4{
    tag "$meta.id"
    label "medium"

    publishDir {"${params.outBase(meta)}/repeatExpansions/kivvi_D4Z4_v0.5/"}, mode: 'copy'


    input:
    tuple val(meta), val(data)
   //  tuple val(meta), path(data)   
    output:
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.kivvi_D4Z4_05")
    
    script:
    """
    ${params.kivvi_dir2}/kivvi \
    -r ${genome_fasta} \
    --bam ${data.bam} \
    -p ${meta.id}.${genome_version}.${readSubset_hifiDefault} \
    -o ${meta.id}.${genome_version}.${readSubset_hifiDefault}.kivvi_D4Z4_05 \
    d4z4
    """
}




process paraphase {

    tag "$meta.id"
    label "medium"
    conda "${params.paraphaseMinimap2}"

    publishDir {"${params.outBase(meta)}/specialAnalysis/paraphase/"},mode: 'copy'

    input:
    tuple val(meta), val(data)
    output:
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.paraphase/*")

    script:
    """
    paraphase \
    -b ${data.bam} \
    --reference ${genome_fasta} \
    -t ${task.cpus} \
    -o ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.paraphase

    python ${localPythonScripts}/flatten_paraphaseSMN.py \
    --json ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.paraphase/${meta.id}.paraphase.json \
    --out ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.paraphase/${meta.id}.${genome_version}.${readSubset_hifiDefault}.paraphase.flattened.tsv

    python ${localPythonScripts}/flatten_paraphaseSMN.py \
    --json ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.paraphase/${meta.id}.paraphase.json \
    --loci SMN1,PMS2,IKBKG \
    --out ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.paraphase/${meta.id}.${genome_version}.${readSubset_hifiDefault}.paraphase.flattened.SMN_PMS2_IKBKG.tsv

     """
}

process paraphase35 {

    tag "$meta.id"
    label "lowCPU"
    conda "${params.paraphase_35}"

    publishDir {"${params.outBase(meta)}/specialAnalysis/paraphase35/"},mode: 'copy'


    input:
    tuple val(meta), val(data)
    output:
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.paraphase/*")
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.paraphaseAnnotate/*")

    script:
    """
    paraphase \
    -b ${data.bam} \
    --reference ${genome_fasta} \
    -t ${task.cpus} \
    -o ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.paraphase

    python ${pbParaphaseAnnotationScript} \
    -i ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.paraphase/${meta.id}.paraphase.json \
    -r rccx,smn1,pms2,strc,cfc1,ikbkg,ncf1,neb,f8,hba,TNXB,OTOA \
    -c ${pbParaphaseConfig} \
    -o ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.paraphaseAnnotate
     """
}


process paraphase4 {

    tag "$meta.id"
    label "lowCPU"
    conda "${params.paraphase40}"

    publishDir {"${params.outBase(meta)}/specialAnalysis/paraphase4/"},mode: 'copy'


    input:
    tuple val(meta), val(data)
    output:
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.paraphase/*")
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.paraphaseAnnotate/*")

    script:
    """
    paraphase \
    -b ${data.bam} \
    --reference ${genome_fasta} \
    -t ${task.cpus} \
    -o ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.paraphase

    python ${pbParaphaseAnnotationScript4} \
    -i ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.paraphase/${meta.id}.paraphase.json \
    -r rccx,smn1,pms2,strc,cfc1,ikbkg,ncf1,neb,f8,hba,TNXB,OTOA \
    -c ${pbParaphaseConfig} \
    -o ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.paraphaseAnnotate
     """
}



process starphase {

    tag "$meta.id"
    label "medium"
    conda "${params.starphase}"
    
    publishDir {"${params.outBase(meta)}/specialAnalysis/starphase/"},mode: 'copy'


    input:
    tuple val(meta), val(data)

    output:
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.starphase.*")
    
    script:
    """
    pbstarphase diplotype \
    --database ${starphase_db} \
    --bam ${data.bam} \
    --reference ${genome_fasta} \
    --vcf ${data.dv_vcf} \
    --sv-vcf ${data.sawfish_vcf} \
    --pharmcat-tsv ${meta.id}.${genome_version}.${readSubset_hifiDefault}.starphase.diplotypes_for_pharmCAT.tsv \
    --output-calls ${meta.id}.${genome_version}.${readSubset_hifiDefault}.starphase.json

    java -jar ${pharmcat_jar} \
    -po ${meta.id}.${genome_version}.${readSubset_hifiDefault}.starphase.diplotypes_for_pharmCAT.tsv \
    -vcf ${data.dv_vcf} \
    -bf ${meta.id}.${genome_version}.${readSubset_hifiDefault}.starphase.pharmCAT \
    -o .
    """
}


process advntr {

    errorStrategy 'ignore'
    tag "$meta.id"
    cpus 8
    publishDir {"${params.outBase(meta)}/advntr/"}, mode: 'copy', pattern: "*.advntr.*"

    conda "${params.advntr15}"

    input:
    tuple val(meta), path(data)
    
    output:
    tuple val(meta), path("${meta.id}.advntr.*")
    
    script:
    """
    advntr genotype \
    -f ${data[0]} \
    --pacbio \
    -m ${vntr_defaultModel} \
    -o ${meta.id}.advntrDefault
    """
}

/////////////////////// TRIO: Variant prioritization with Exomiser 14.1.0 ///////////////////////

process exo14_2508_exome {
    label "medium"
    tag "$meta.caseID"

    publishDir {"${params.outBase(meta)}/exomiser14_2508/exomiser/"}, mode: 'copy'
    publishDir {"${params.outBase(meta)}/documents/"}, mode: 'copy',pattern:"*.{hpo.txt,yml,ped}"
    input:
    tuple val(meta), path(vcf), path(idx), path(hpo), path(samplesheet)

    output:
    path("*.{html,tsv,vcf,json,hpo.txt,yml,ped}")

    script:
    """
    python3 ${localPythonScripts}/make_ped_and_family_v2.py \
    --samplesheet ${samplesheet} \
    --vcf ${vcf} \
    --caseid ${meta.caseID} \
    --hpo ${hpo}

    java -jar ${localProgramPath}/exomiser-cli-14.1.0/exomiser-cli-14.1.0.jar \
    --sample ${meta.caseID}-family.yml \
    --analysis ${exome_yml} \
    --spring.config.location=${localProgramPath}/exomiser-cli-14.1.0/
    
    mv results/* .
    mv exomiser_tmp.html ${meta.caseID}.exo14_2508.html
    mv exomiser_tmp.variants.tsv ${meta.caseID}.exo14_2508.Variants.tsv
    mv exomiser_tmp.genes.tsv ${meta.caseID}.exo14_2508.Genes.tsv
    mv exomiser_tmp.json ${meta.caseID}.exo14_2508.json
    """

}

process exo14_2508_genome {
    label "medium"
    tag "$meta.caseID"

    publishDir {"${params.outBase(meta)}/exomiser14_2508/genomiser/"}, mode: 'copy'

    input:
    tuple val(meta), path(vcf), path(idx), path(hpo), path(samplesheet)

    output:
    path("*.{html,tsv,vcf,json}")

    script:
    """
    python3 ${localPythonScripts}/make_ped_and_family_v2.py \
    --samplesheet ${samplesheet} \
    --vcf ${vcf} \
    --caseid ${meta.caseID} \
    --hpo ${hpo}
    
    java -jar ${localProgramPath}/exomiser-cli-14.1.0/exomiser-cli-14.1.0.jar \
    --sample ${meta.caseID}-family.yml \
    --analysis ${genome_yml} \
    --spring.config.location=${localProgramPath}/exomiser-cli-14.1.0/
    
    mv results/* .
    mv exomiser_tmp.html ${meta.caseID}.genomiser14_2508.html
    mv exomiser_tmp.variants.tsv ${meta.caseID}.genomiser14_2508.Variants.tsv
    mv exomiser_tmp.genes.tsv ${meta.caseID}.genomiser14_2508.Genes.tsv
    mv exomiser_tmp.json ${meta.caseID}.genomiser14_2508.json
    """
}

process exo14_2508_SV {
    label "medium"
    tag "$meta.caseID"

    publishDir {"${params.outBase(meta)}/exomiser14_2508/exomiserStructuralVariants/"}, mode: 'copy'

    input:
    tuple val(meta), path(vcf), path(idx), path(hpo), path(samplesheet)

    output:
    path("*.{html,tsv,vcf,json,hpo.txt,yml,ped}")

    script:
    """
    zcat ${vcf} | sed 's/^##fileformat=VCFv4\\.4/##fileformat=VCFv4.2/'| bgzip > ${meta.caseID}.sawfish.forExomiser.vcf.gz

    python3 ${localPythonScripts}/make_ped_and_family_v2.py \
    --samplesheet ${samplesheet} \
    --vcf ${meta.caseID}.sawfish.forExomiser.vcf.gz \
    --caseid ${meta.caseID} \
    --hpo ${hpo}

    java -jar ${localProgramPath}/exomiser-cli-14.1.0/exomiser-cli-14.1.0.jar \
    --sample ${meta.caseID}-family.yml \
    --analysis ${exome_yml} \
    --spring.config.location=${localProgramPath}/exomiser-cli-14.1.0/
    
    mv results/* .
    mv exomiser_tmp.html ${meta.caseID}.SVs.exo14_2508.html
    mv exomiser_tmp.variants.tsv ${meta.caseID}.SVs.exo14_2508.Variants.tsv
    mv exomiser_tmp.genes.tsv ${meta.caseID}.SVs.exo14_2508.Genes.tsv
    mv exomiser_tmp.json ${meta.caseID}.SVs.exo14_2508.json
    """

}


///////////////////////////////////////////////////
/////// ------- METHYLATION ------- ///////////////
///////////////////////////////////////////////////
process pbCPGtools {
    
    tag "$meta.id"
    label "medium"
    conda "${params.pbCPGtools}"

    publishDir {"${params.outBase(meta)}/specialAnalysis/methylation/BigWigBed/"}, mode: 'copy', pattern: "*.methylation.{hap1,hap2,combined}.*"

    publishDir "${lrsStorage}/methylation/pbCpGtools/${meta.id}/", mode: 'copy', pattern:"*.bed.*"

    input:
    tuple val(meta), val(data)
    
    output:
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.methylation*")
    
    script:
    """
    aligned_bam_to_cpg_scores \
    --bam ${data.bam} \
    --output-prefix ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.methylation
    """
}

process methBat{
    tag "$meta.id"
    label "medium"
    conda "${params.methbat}"

    publishDir {"${params.outBase(meta)}/specialAnalysis/methylation/"}, mode: 'copy'

    publishDir "${lrsStorage}/methylation/methBatProfiles/", mode: 'copy', pattern:"*.profile"


    input:
    tuple val(meta), path(data)
    
    output:
    tuple val(meta), path("*.met.*")
    
    script:
    """
    methbat segment \
    --input-prefix ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.methylation \
    --output-prefix ${meta.id}.${genome_version}.${readSubset_hifiDefault}.met.segments

    methbat profile \
    --input-prefix ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.methylation \
    --input-regions ${methylationBackground} \
    --output-region-profile ${meta.id}.${genome_version}.${readSubset_hifiDefault}.met.profile

    methbat profile \
    --input-prefix ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.methylation \
    --input-regions ${methylationBackgroundLocal} \
    --output-region-profile ${meta.id}.${genome_version}.${readSubset_hifiDefault}.met.profileLOCAL

    methbat report \
    --input-prefix ${meta.id}.${genome_version}.${readSubset_hifiDefault}.hiphase.methylation \
    --input-regions ${methylationICRegions} \
    --output-report ${meta.id}.${genome_version}.${readSubset_hifiDefault}.met.imprintingReport.tsv 

    """
}

process methBatNEW_pileup{
    tag "$meta.id"
    label "intermediateCPU"
    conda "${params.methbat_v1}"

    publishDir {"${params.outBase(meta)}/specialAnalysis/methylation/5mC_pileup/"},   mode: 'copy',   pattern: "*.5mC.bed.*"
    publishDir {"${params.outBase(meta)}/specialAnalysis/methylation/5mC_bedgraphs/"},   mode: 'copy',   pattern: "*.5mC.bedgraph.*"

    publishDir "${lrsStorage}/methylationNEW/5mC_pileup/",   mode: 'copy',   pattern: "*.5mC.bed.*"

  //  publishDir {"${params.outBase(meta)}/specialAnalysis/methylation/5hmC/"},  mode: 'copy',   pattern: "*.5hmC.bed.*"
   // publishDir {"${params.outBase(meta)}/specialAnalysis/methylation/6mA/"},   mode: 'copy',   pattern: "*.6mA.bed.*"

    input:
    tuple val(meta), val(data)
    
    output:
    tuple val(meta), path("*.met.*"), path("*.5mC.bedgraph.*")
    tuple val(meta), path("*.5mC.bed.gz"),  path("*.5mC.bed.gz.tbi"),   emit: met5mC
    tuple val(meta), path("*.5hmC.bed.gz"), path("*.5hmC.bed.gz.tbi"),  emit: met5hmC
    tuple val(meta), path("*.6mA.bed.gz"),  path("*.6mA.bed.gz.tbi"),   emit: met6mA
   
    script:
    """
    methbat pileup \
    --threads ${task.cpus} \
    --input-bam ${data.bam} \
    --output-prefix ${meta.id}.${genome_version}.${readSubset_hifiDefault}.met.pileup

    zgrep "Total" ${meta.id}.${genome_version}.${readSubset_hifiDefault}.met.pileup.5mC.bed.gz | \
    cut -f 1-3,7 | \
    bgzip > ${meta.id}.${genome_version}.${readSubset_hifiDefault}.5mC.bedgraph.gz

    tabix -p bed ${meta.id}.${genome_version}.${readSubset_hifiDefault}.5mC.bedgraph.gz

    """
}

process methBatNEW_profile_single {
    tag "$meta.id"
    label "low"
    conda "${params.methbat_v1}"

    publishDir {"${params.outBase(meta)}/specialAnalysis/methylation/5mC_profile/"},   mode: 'copy',   pattern: "*.5mC.cpgIslands.profile.tsv"
    publishDir "${lrsStorage}/methylationNEW/5mC_CGI_profiles/", mode: 'copy', pattern:"*.profile.tsv"
    input:
    tuple val(meta), path(data), path(tbi)
    
    output:
    tuple val(meta), path("*.5mC.cpgIslands.profile.tsv")

    script:
    """
    methbat profile \
    --input-regions ${methylationCpG_regions} \
    --input-pileup ${data} \
    --output-region-profile ${meta.id}.${genome_version}.${readSubset_hifiDefault}.met.5mC.cpgIslands.profile.tsv
    """
}

///////////////////////////////////////////////////
/////// ------- QUALITY CONTROL ------- ///////////
///////////////////////////////////////////////////

process mosdepthROI {
    tag "$meta.id"
    label "low"
    conda "${params.mosdepth}"

    publishDir {"${params.outBase(meta)}/QC/mosdepth/"}, mode: 'copy'

    input: 
    tuple val(meta), val(data)  

    output:
    tuple val(meta), path("${meta.id}.${genome_version}_roi.*"),emit: mosdepth_roi
    tuple val(meta), path("*.region.dist.txt"), emit:multiqc
    script:
    def callable=params.genome=="hg38" ? "--by ${CALLABLE_ROI}" : "--by 1000"
    """
    mosdepth \
    -t ${task.cpus} \
    $callable \
    ${meta.id}.${genome_version}_roi \
    ${data.mainBamFile}

    """
}


process whatsHap_stats {
    tag "$meta.id"
    label "low"
    conda "${params.whatshap}" 

    publishDir {"${params.outBase(meta)}/QC/whatsHap/"}, mode: 'copy'

    input: 
    tuple val(meta), val(data)  
    output:
    tuple val(meta), path("${meta.id}.whatshap.stats.tsv"),emit:multiqc

    script:
    """
    whatshap stats \
    ${data.dv_vcf} \
    --tsv=${meta.id}.whatshap.stats.tsv
    """
}

process cramino {
    tag "$meta.id"
    label "low"
    conda "${params.cramino}"

    publishDir {"${params.outBase(meta)}/QC/cramino/"}, mode: 'copy'

    input: 
    tuple val(meta), val(data)  // meta: [npn,datatype,sampletype,id], data: [cram,crai]

    output:
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.craminoQC.txt")

    script:
    """
    cramino \
    -t ${task.cpus} \
    --karyotype \
    --phased \
    ${data.bam} > ${meta.id}.${genome_version}.${readSubset_hifiDefault}.craminoQC.txt
    """
}

process nanoStat {
    tag "$meta.id"
    label "low"
    conda "${params.nanostats}"

    publishDir {"${params.outBase(meta)}/QC/nanoStat/"}, mode: 'copy'

    input: 
    tuple val(meta), val(data) 

    output:
    tuple val(meta), path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.nanostat.txt"),emit: multiqc
    path("${meta.id}.${genome_version}.${readSubset_hifiDefault}.nanostat.txt")
    script:
    """
    NanoStat \
    -t ${task.cpus} \
    -n ${meta.id}.${genome_version}.${readSubset_hifiDefault}.nanostat.txt \
    --bam ${data.mainBamFile}
    """
}

process multiQC {
    tag { params.layoutMode == 'jointAnalysis' ? meta.caseID : meta.id }
    label "low"
    conda "${params.multiqc}"

    publishDir {"${params.outBase(meta)}/QC/"}, mode: 'copy'

    input:
    tuple val(meta),  path(qcfiles)  

    output:
    path ("*MultiQC*.html")

    script:
    def reportName = (params.layoutMode == 'jointAnalysis') ? "${meta.caseID}.MultiQC.DNA.html" : "${meta.id}.MultiQC.DNA.html"

    """
    mkdir -p qc_in
    cp -L ${qcfiles} qc_in/
    
    multiqc \
    -c ${multiqc_config} \
    -f -q qc_in \
    -n ${reportName}
    """
}

//${outputDirBase}/${meta.caseID}/${meta.outKey}/${meta.rekv}_${meta.id}_${meta.groupKey}_${readSet}/QC/
//    -f -q ${launchDir}/${outputDir}/${meta.caseID}/${meta.outKey}/${meta.rekv}_${meta.id}_${meta.groupKey}_${readSet}/QC/ \


process multiQC_ALL {
    label "low"
    conda "${params.multiqc}"

    publishDir "${outputDirBase}/runInfo/${date}_${ssBase}/", mode: 'copy'

    when:
    params.groupedOutput

    input:
    tuple val(meta),path(data)  

    output:
    path ("${params.rundir}.MultiQC.ALL.html")


    script:
    //def qcdir = params.groupedOutput ? "${launchDir}/${outputDir}/*/QC/" : "${launchDir}/${outputDir}/*/*/QC/"
    
    """
    multiqc \
    -c ${multiqc_config} \
    -f -q . \
    -n ${params.rundir}.MultiQC.ALL.html
    """
}


process build_symlinks {
    tag "build_symlinks"

    // No input channels are required; Nextflow will wait until all upstream processes are done
    // before scheduling this process, if we make it the last step in the workflow.

    output:
    path "symlinks.done"

    script:
    """
    ${localBashScripts}/createSymlinks_after_nextflow.sh ${testLinksInput} ${testLinksOutput}
    touch symlinks.done
    """
}





////////////// END TO DO END ///////////////////




///////////////////////////////////////////////////
////// ------- DE NOVO ASSEMBLY ------- ///////////
///////////////////////////////////////////////////


process hifiasm {
    errorStrategy 'ignore'
    tag "$meta.id"
    cpus 8
    publishDir {"${params.outBase(meta)}/hifiasm/"}, mode: 'copy', pattern: "*.advntr.*"

    conda "${params.hifiasm}"

    input:
    tuple val(meta), path(data)
    
    output:
    tuple val(meta), path("${meta.id}.advntr.*")
    
    script:
    """
    samtools fastq \
    -@ 12 \
    ${data[0]} > ${meta.id}.hifireads.fastq
    hifiasm \
    -t ${task.cpus} \
    -o ${meta.id}.asm \
    ${meta.id}.hifireads.fastq

    """
}



